apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: libs.plugins.ktlint.get().pluginId

ext.loadEnv = {
    def file = rootDir.toPath().resolve(".env").toFile()

    def envMap = [:]

    if (!file.exists()) {
        return envMap
    }

    println("Loading environment from ${file.path}")

    file.readLines().each() {
        if (!it.isEmpty() && !it.startsWith("#")) {
            def (key, value) = it.tokenize('=')
            envMap[key] = value
        }
    }

    println("Loaded environment with length ${envMap.size()}")

    return envMap
}

ext.getEnv = { key ->
    return System.getenv(key) ?: env[key]
}

ext {
    language = project.parent.name
    source = project.name

    env = loadEnv()
}

assert !ext.has("pkgNameSuffix")
assert !ext.has("libVersion")

assert sourceName.chars().max().asInt < 0x180 : "Extension name should be romanized"

android {
    namespace = "com.jramberger.mysteries.extension"
    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    compileSdk = 34

    defaultConfig {
        minSdk = 21
        consumerProguardFiles("consumer-rules.pro")

        versionCode sourceVersionCode
        versionName "1.0.$versionCode"

        applicationIdSuffix language + "." + source

        base {
            archivesName = "mysteries-ext-$language-$source-$versionName"
        }

        assert sourceClass.startsWith(".")

        manifestPlaceholders = [
            appName : "Mysteries Source - $sourceName",
            extClass: sourceClass,
        ]
    }

    signingConfigs {
        release {
            storeFile rootProject.file("signingkey.jks")
            storePassword getEnv("KEY_STORE_PASSWORD")
            keyAlias getEnv("ALIAS")
            keyPassword getEnv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            vcsInfo.include false
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    buildFeatures {
        buildConfig true
    }

    packaging {
        resources.excludes.add("kotlin-tooling-metadata.json")
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation(project(":base"))
    compileOnly(libs.bundles.all)
}

tasks.register("writeManifestFile") {
    doLast {
        def manifest = android.sourceSets.getByName("main").manifest
        if (!manifest.srcFile.exists()) {
            // make build directory if it doesn't exist
            layout.buildDirectory.get().getAsFile().mkdirs()
            File tempFile = layout.buildDirectory.get().file("tempAndroidManifest.xml").getAsFile()
            if (!tempFile.exists()) {
                tempFile.withWriter {
                    it.write('<?xml version="1.0" encoding="utf-8"?>\n<manifest />\n')
                }
            }
            manifest.srcFile(tempFile.path)
        }
    }
}

preBuild.dependsOn(writeManifestFile)
